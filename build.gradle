plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.client_version + (System.getenv("CIRCLE_BUILD_NUM") != null ? ("-" + System.getenv("CIRCLE_BUILD_NUM")) : "")
group = project.maven_group

repositories {
    // GitHub
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }

    maven {
        url 'https://m2.dv8tion.net/releases'
    }

    // Meteor
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org"
    }

    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }

    // Litematica
    maven {
        url = "https://www.cursemaven.com"
    }

    maven {
        url 'https://masa.dy.fi/maven'
    }

    // Other
    maven {
        name = "dblsaiko"
        url = "https://maven.dblsaiko.net/"
    }

    jcenter()
}

dependencies {
    def library = { id ->
        implementation id
        shadow id
    }

    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Compat fixes
    modCompileOnly fabricApi.module("fabric-renderer-indigo", project.api_version)
    modCompileOnly("me.jellysquid.mods:sodium-fabric:${project.sodium_version}") {
        transitive = false
    }
    modCompileOnly("grondag:canvas-mc117-1.17:1.0.+") {
        transitive = false
    }

    // Baritone integration
    modImplementation "baritone:baritone:${project.baritone_version}"
    include "baritone:baritone:${project.baritone_version}"

    // Seed integration
    modImplementation "com.github.hube12:SEED:${project.seed_version}"
    include "com.github.hube12:SEED:${project.seed_version}"

    // Litematica integration
    modImplementation "curse.maven:litematica-${project.litematica_project_id}:${project.litematica_file_id}"
    include "curse.maven:litematica-${project.litematica_project_id}:${project.litematica_file_id}"

    // Malilib for Litematica
    modImplementation "fi.dy.masa.malilib:malilib-fabric-${project.minecraft_version}:${project.malilib_version}"
    include "fi.dy.masa.malilib:malilib-fabric-${project.minecraft_version}:${project.malilib_version}"

    // Dependencies
    library "com.github.Vatuu:discord-rpc:${project.discord_version}"

    // Music

    // Lava Player
    modImplementation "com.github.Walkyst:lavaplayer:${project.lavaplayer_version}"
    include "com.github.Walkyst:lavaplayer:${project.lavaplayer_version}"
    library "com.sedmelluq:lava-common:${project.lava_common_version}"
    library "com.sedmelluq:lavaplayer-natives:${project.lavaplayer_natives_version}"

    // Other
    library "org.apache.httpcomponents:httpclient:${project.apache_http_client_version}"
    library "com.fasterxml.jackson.core:jackson-core:${project.jackson_core_version}"
    library "com.fasterxml.jackson.core:jackson-databind:${project.jackson_databind_version}"
    library "com.fasterxml.jackson.core:jackson-annotations:${project.jackson_annotations_version}"
    library "org.jsoup:jsoup:${project.jsoup_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/mathaxlegacy.accesswidener")
}

shadowJar {
    configurations = [ project.configurations.shadow ]
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
        filter {
            line -> line.replace("@mc_version@", project.minecraft_version)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

jar {
    from "LICENSE"

    manifest {
        attributes("Main-Class": "mathax.legacy.installer.Installer")
    }
}

remapJar {
    dependsOn shadowJar
    input.set(shadowJar.archiveFile)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifact(javadocJar) {
                builtBy javadocJar
            }
        }
    }
}

apply from: "scripts/packetutils.gradle"
